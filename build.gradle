buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
		classpath 'me.tatarka:gradle-retrolambda:3.2.0'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'

group = "com.unascribed"
archivesBaseName = "Laminate"
version = "0.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

retrolambda {
	javaVersion JavaVersion.VERSION_1_6
}

repositories {
	mavenCentral()
	maven {
		name = 'sonatype-nexus'
		url = 'https://oss.sonatype.org/content/repositories/public/'
	}
}

configurations {
	deploy
	shade
	compile.extendsFrom shade
}

dependencies {
	deploy "org.apache.maven.wagon:wagon-ssh:2.10"
	
	shade files (
		'common/build/libs/LaminateCommon.jar',
		'version-specific/1.7.10/build/libs/LaminateOneSeven.jar',
		'version-specific/1.8/build/libs/LaminateOneEight.jar'
	)
	shade 'com.github.gfx.util:weak-identity-hash-map:2.0.1'
}

jar {
	manifest {
		attributes (
			'FMLAT': 'laminate_at.cfg'
		)
	}
	configurations.shade.each { dep ->
		from(project.zipTree(dep)){
			exclude 'META-INF', 'META-INF/**'
		}
	}
}

minecraft {
	version = "1.8.9-11.15.0.1715"
	mappings = "snapshot_20160130"
	
	runDir = "eclipse"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
				
		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
		
	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

def parseConfig(File config) {
	config.withReader {
		def prop = new Properties()
		prop.load(it)
		return (new ConfigSlurper().parse(prop))
	}
}

ext.priv = parseConfig(file('private.properties'))

uploadArchives {
	repositories.mavenDeployer {
		configuration = configurations.deploy
		pom.artifactId = 'laminate'
		repository(url: "scp://"+priv.scpHost+priv.scpPath+"/releases") {
			authentication(userName: priv.scpUser, password: priv.scpPass)
		}
	}
}
